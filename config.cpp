#include "config.h"
float Gain = 2;
File file;
uint8_t buffer[BUFFER_SIZE];
size_t bytesRead;
int touchValue = 0;
String tmp="test";

// -------------------- WIFI --------------------
const char* ssid = "LAPTOP-GDFAD7NL4364";
const char* password = "878p21}H";
const char* host = "esp32-test-prs1.onrender.com";  // å»æ‰ https://
const int httpsPort = 443;
const char* urlPath2 = "/tts";
const char* urlPath = "/upload";
const String apiKey = "AIzaSyDuBKdiK4b4WO6XuiAOZ9DsP1WinfGPfrs";  // ä½ çš„ Gemini API Key


// OLED åˆå§‹åŒ–
U8G2_SSD1306_128X64_NONAME_F_HW_I2C u8g2(U8G2_R0, /* reset=*/U8X8_PIN_NONE, OLED_CLOCK, OLED_DATA);

// -------------------- I2S åˆå§‹åŒ–å‡½å¼ --------------------
void setupI2SInput() {
    const i2s_config_t i2s_config = {
        .mode = (i2s_mode_t)(I2S_MODE_MASTER | I2S_MODE_RX),
        .sample_rate = SAMPLE_RATE,
        .bits_per_sample = (i2s_bits_per_sample_t)BITS_PER_SAMPLE,
        .channel_format = I2S_CHANNEL_FMT_ONLY_LEFT,
        .communication_format = (i2s_comm_format_t)(I2S_COMM_FORMAT_I2S),
        .intr_alloc_flags = ESP_INTR_FLAG_LEVEL1,
        .dma_buf_count = 4,
        .dma_buf_len = 1024,
        .use_apll = false,
        .fixed_mclk = 0
    };

    const i2s_pin_config_t pin_config = {
        .mck_io_num = -1,
        .bck_io_num = I2S_SCK,
        .ws_io_num = I2S_WS,
        .data_out_num = I2S_PIN_NO_CHANGE,
        .data_in_num = I2S_SD
    };

    i2s_driver_install(I2S_PORT_RX, &i2s_config, 0, NULL);
    i2s_set_pin(I2S_PORT_RX, &pin_config);
    i2s_zero_dma_buffer(I2S_PORT_RX);
}

void setupI2SOutput() {
    const i2s_config_t i2s_config = {
        .mode = (i2s_mode_t)(I2S_MODE_MASTER | I2S_MODE_TX),
        .sample_rate = SAMPLE_RATE,
        .bits_per_sample = (i2s_bits_per_sample_t)BITS_PER_SAMPLE,
        .channel_format = I2S_CHANNEL_FMT_ONLY_LEFT,
        .communication_format = (i2s_comm_format_t)(I2S_COMM_FORMAT_I2S),
        .intr_alloc_flags = 0,
        .dma_buf_count = 4,
        .dma_buf_len = 1024,
        .use_apll = false,
        .fixed_mclk = 0
    };

    const i2s_pin_config_t pin_config = {
        .mck_io_num = -1,
        .bck_io_num = I2S_BCLK,
        .ws_io_num = I2S_LRC,
        .data_out_num = I2S_DOUT,
        .data_in_num = I2S_PIN_NO_CHANGE
    };

    i2s_driver_install(I2S_PORT_TX, &i2s_config, 0, NULL);
    i2s_set_pin(I2S_PORT_TX, &pin_config);
    i2s_zero_dma_buffer(I2S_PORT_TX);
}

void writeWavHeader(File &file, int sampleRate, int bitsPerSample, uint32_t numSamples) {
  int byteRate = sampleRate * bitsPerSample / 8;
  int dataSize = numSamples * bitsPerSample / 8;

  file.seek(0);  // ç¢ºä¿å¾æª”æ¡ˆé–‹é ­å¯«
  file.write((const uint8_t *)"RIFF", 4);
  int chunkSize = dataSize + 36;
  file.write((const uint8_t *)&chunkSize, 4);
  file.write((const uint8_t *)"WAVEfmt ", 8);

  int subchunk1Size = 16;
  short audioFormat = 1;
  short numChannels = 1;
  file.write((const uint8_t *)&subchunk1Size, 4);
  file.write((const uint8_t *)&audioFormat, 2);
  file.write((const uint8_t *)&numChannels, 2);
  file.write((const uint8_t *)&sampleRate, 4);
  file.write((const uint8_t *)&byteRate, 4);

  short blockAlign = bitsPerSample / 8;
  file.write((const uint8_t *)&blockAlign, 2);
  file.write((const uint8_t *)&bitsPerSample, 2);

  file.write((const uint8_t *)"data", 4);
  file.write((const uint8_t *)&dataSize, 4);
}

void recording() {
  Serial.println("Start recording...");
  showWords("Start recording...");
  // åˆªé™¤èˆŠæª”æ¡ˆï¼ˆå¦‚æœå­˜åœ¨ï¼‰
  if (SD.exists("/record.wav")) {
    SD.remove("/record.wav");
    Serial.println("Old /record.wav removed.");
    showWords("Old /record.wav removed.");
  }
  file = SD.open("/record.wav", FILE_WRITE);
  if (!file) {
    Serial.println("Failed!");
    showWords("Failed!");
    return;
  }
  writeWavHeader(file, SAMPLE_RATE, BITS_PER_SAMPLE, 0);
  while (touchValue < 40) {
    i2s_read(I2S_PORT_RX, buffer, BUFFER_SIZE, &bytesRead, 0);
    // æ”¾å¤§éŸ³é‡
    for (size_t i = 0; i < bytesRead; i += 2) {
      int16_t sample = buffer[i] | (buffer[i + 1] << 8);
      int32_t amplified = (int32_t)(sample * Gain);
      if (amplified > 32767) amplified = 32767;
      if (amplified < -32768) amplified = -32768;
      buffer[i] = amplified & 0xFF;
      buffer[i + 1] = (amplified >> 8) & 0xFF;
    }
    file.write(buffer, bytesRead);
    touchValue = touchRead(TOUCH_PIN);
  }
  // æ”¾é–‹åœæ­¢éŒ„éŸ³
  Serial.println("Stop recording...");
  showWords("Stop recording...");
  // æ›´æ–° WAV header
  uint32_t numSamples = file.size() - 44;
  numSamples /= (BITS_PER_SAMPLE / 8);
  writeWavHeader(file, SAMPLE_RATE, BITS_PER_SAMPLE, numSamples);
  file.close();
  Serial.println("Saved as /record.wav");
  showWords("Saved as /record.wav");
}

void playAudio() {
  File file = SD.open("/speech.wav");
  if (!file) {
    Serial.println("Failed to open file for reading!");
    showWords("Failed to open file for reading!");
    return;
  }

  // è·³é WAV æ¨™é ­ (44 bytes)
  file.seek(44);
  uint8_t buffer[BUFFER_SIZE];
  size_t bytesWritten;

  digitalWrite(DAC_SD, HIGH);
  Serial.println("Playing...");
  showWords("Playing...");
  while (file.available()) {
    size_t bytesRead = file.read(buffer, BUFFER_SIZE);
    i2s_write(I2S_PORT_TX, buffer, bytesRead, &bytesWritten, portMAX_DELAY);
  }

  file.close();
  digitalWrite(DAC_SD, LOW);
  Serial.println("Playback finished.");
  showWords("Playback finished.");
}

void showWords(const char *str) {
  u8g2.clearBuffer();                                  // æ¸…ç©ºç•«é¢ buffer
  u8g2.setFont(u8g2_font_6x10_tf);          // ä¸­æ–‡å­—å‹              
  const uint8_t lineHeight = u8g2.getMaxCharHeight();  // æ¯è¡Œé«˜åº¦
  const uint8_t screenWidth = 128;                     // OLED å¯¬åº¦
  const uint8_t screenHeight = 64;                     // OLED é«˜åº¦
  uint8_t x = 0;
  uint8_t y = lineHeight;

  const char *p = str;
  char line[128];  // æš«å­˜ä¸€è¡Œæ–‡å­—
  uint8_t lineLen = 0;

  while (*p) {
    line[lineLen++] = *p;

    // è¨ˆç®—ç›®å‰ line çš„å¯¬åº¦
    line[lineLen] = '\0';
    int16_t w = u8g2.getStrWidth(line);

    if (w > screenWidth || *p == '\n') {
      // è¶…å‡ºå¯¬åº¦æˆ–é‡åˆ°æ›è¡Œç¬¦è™Ÿå°±æ›è¡Œ
      line[lineLen - 1] = '\0';  // ç§»é™¤æœ€å¾Œä¸€å€‹å­—ï¼ˆæˆ–æ›è¡Œç¬¦è™Ÿï¼‰
      u8g2.setCursor(x, y);
      u8g2.print(line);
      y += lineHeight;

      if (y > screenHeight) break;  // è¶…å‡ºè¢å¹•ç¯„åœåœæ­¢

      lineLen = 0;                           // æ¸…ç©ºæš«å­˜è¡Œ
      if (*p != '\n') line[lineLen++] = *p;  // å°‡è¶…å‡ºå­—æ”¾å…¥ä¸‹ä¸€è¡Œ
    }

    p++;
  }

  // å°å‡ºæœ€å¾Œä¸€è¡Œ
  if (lineLen > 0 && y <= screenHeight) {
    line[lineLen] = '\0';
    u8g2.setCursor(x, y);
    u8g2.print(line);
  }

  u8g2.sendBuffer();  // å°‡ buffer å‚³åˆ° OLED
}

void send_wav() {

  File file = SD.open("/record.wav", FILE_READ);
  if (!file) {
    Serial.println("âŒ Failed to open record.wav");
    showWords("Failed to open record.wav");
    return;
  }
  // å»ºç«‹ HTTPS é€£ç·š
  WiFiClientSecure client;
  client.setInsecure();  // å¿½ç•¥æ†‘è­‰ï¼ˆæ¸¬è©¦ç”¨ï¼‰
  if (!client.connect(host, httpsPort)) {
    Serial.println("âŒ Connection failed!");
    showWords("Connection failed!");
    file.close();
    return;
  }

  // çµ„ multipart header & footer
  String head = "--" + String(BOUNDARY) + "\r\nContent-Disposition: form-data; name=\"file\"; filename=\"record.wav\"\r\nContent-Type: audio/wav\r\n\r\n";
  String tail = "\r\n--" + String(BOUNDARY) + "--\r\n";

  // è¨ˆç®— Content-Length
  size_t contentLength = head.length() + file.size() + tail.length();

  // é€ HTTP POST è«‹æ±‚é ­
  client.printf("POST %s HTTP/1.1\r\n", urlPath);
  client.printf("Host: %s\r\n", host);
  client.println("User-Agent: ESP32");
  client.printf("Content-Type: multipart/form-data; boundary=%s\r\n", BOUNDARY);
  client.printf("Content-Length: %u\r\n\r\n", contentLength);

  // é€ head
  client.print(head);

  // åˆ†æ®µé€æª”æ¡ˆï¼ˆé¿å…ä¸€æ¬¡è®€å®Œå ç”¨å¤ªå¤š RAMï¼‰
  const size_t bufSize = 512;
  uint8_t buf[bufSize];
  while (file.available()) {
    size_t len = file.read(buf, bufSize);
    client.write(buf, len);
  }

  // é€ tail
  client.print(tail);

  Serial.println("ğŸ“¡ Request sent, waiting response...");
  showWords("Request sent, waiting response...");

  // è®€å›æ‡‰
  while (client.connected()) {
    String line = client.readStringUntil('\n');
    if (line == "\r") break;  // headers çµæŸ
  }

  String response;
  while (client.available()) {
    response += (char)client.read();
  }
  // è§£æ JSON
  DynamicJsonDocument doc(512);  // è¨˜å¾—æ ¹æ“š JSON å¤§å°èª¿æ•´
  DeserializationError error = deserializeJson(doc, response);

  int idx = response.indexOf('{');
  if (idx >= 0) {
    String jsonStr = response.substring(idx);
    DynamicJsonDocument doc(512);
    if (deserializeJson(doc, jsonStr) == DeserializationError::Ok) {
      const char* text = doc["text"];
      tmp=String(text);
      Serial.println("ğŸ¯ Text only: " + tmp);
      showWords(text);
    }
  }

  file.close();
  delay(1000);
}

void send_to_llm() {
  String userText = tmp;
  HTTPClient http;
  http.setReuse(false);  // æ¯æ¬¡è«‹æ±‚éƒ½é‡æ–°é€£ç·š
  String url = "https://generativelanguage.googleapis.com/v1/models/gemini-2.0-flash:generateContent?key=" + apiKey;
  http.begin(url);
  http.addHeader("Content-Type", "application/json");

  // çµ„æˆ JSON body
  // String payload = "{ \"contents\": [{ \"parts\": [{ \"text\": \"" + userText + "\" }] }] }";
  String payload = "{ \"contents\": [{ \"parts\": [{ \"text\": \"" + userText + "\" }] }], \"generationConfig\": { \"maxOutputTokens\": 50 } }";
  int httpCode = http.POST(payload);
  if (httpCode > 0) {
    String response = http.getString();
    // Serial.println("Raw Response:");
    // Serial.println(response);  // è‹¥è¦ debug å¯ä»¥å°å‡ºå®Œæ•´ JSON

    // è§£æ JSON
    DynamicJsonDocument doc(8192);  // Gemini response æœ‰æ™‚å€™æ¯”è¼ƒé•·
    DeserializationError error = deserializeJson(doc, response);
    if (!error) {
      if (doc.containsKey("candidates")) {
        const char* generatedText = doc["candidates"][0]["content"]["parts"][0]["text"];
        if (generatedText) {
          tmp=String(generatedText);
          Serial.println("Generated Text:");
          Serial.println(generatedText);
          showWords(generatedText);
        } else {
          Serial.println("âš ï¸ å›æ‡‰ä¸­æ²’æœ‰ç”Ÿæˆæ–‡å­—");
        }
      } else {
        Serial.println("âš ï¸ æ²’æœ‰æ‰¾åˆ° candidates");
      }
    } else {
      Serial.println("JSON parse error");
    }
  } else {
    Serial.print("HTTP request failed, code: ");
    Serial.println(httpCode);
  }

  http.end();
}

void downloadTTS() {
  WiFiClientSecure client;
  client.setInsecure();  // æ¸¬è©¦ç”¨ï¼Œè·³éè­‰æ›¸é©—è­‰

  HTTPClient https;
  String url = String("https://") + host + urlPath2;
  https.begin(client, url);
  https.addHeader("Content-Type", "application/x-www-form-urlencoded");

  String payload = "text=" + tmp + "&lang=en";
  int httpCode = https.POST(payload);

  if (httpCode == 200) {
    Serial.println("TTS request successful, downloading WAV...");

    File file = SD.open(WAV_FILE, FILE_WRITE);
    if (!file) {
      Serial.println("Failed to open file on SD");
      https.end();
      return;
    }

    // ä½¿ç”¨ getStream()ï¼Œåˆ†æ®µè®€å–
    WiFiClient* stream = https.getStreamPtr();
    int len = https.getSize();  // å›å‚³è³‡æ–™é•·åº¦
    Serial.println("Content length: " + String(len));

    uint8_t buffer[512];
    int bytesRead = 0;

    while (https.connected() && (bytesRead < len)) {
      size_t sizeAvailable = stream->available();
      if (sizeAvailable) {
        int c = stream->readBytes(buffer, ((sizeAvailable > sizeof(buffer)) ? sizeof(buffer) : sizeAvailable));
        file.write(buffer, c);
        bytesRead += c;
      }
      delay(1);
    }
    file.close();
    Serial.println("WAV saved to SD: " + String(WAV_FILE));
  }
}

